# Tell autoconf we're compiling a C++ program using automake and libtool.
AC_INIT(adplay,1.2)
AC_CONFIG_SRCDIR(adplay.cc)
AC_CONFIG_FILES(Makefile)
AM_INIT_AUTOMAKE
AM_CONFIG_HEADER(config.h)
AM_PROG_LIBTOOL
AC_LANG(C++)

# Check if we got a sane C/C++ compilation environment.
AC_PROG_INSTALL
AC_PROG_CC
AC_PROG_CXX

# Nothing works without the AdPlug core library...
AC_CHECK_LIB(adplug,main,,[AC_MSG_ERROR([AdPlug not found!])])

##### Output mechanism checks #####
# These checks enable or disable certain output mechanisms,
# depending on system facilities and user requests.

AC_MSG_NOTICE([Configuring output mechanisms:])

# Define user option arguments
AC_ARG_ENABLE([output-oss],AC_HELP_STRING([--disable-output-oss],[Disable OSS output]))
AC_ARG_ENABLE([output-null],AC_HELP_STRING([--disable-output-null],[Disable null output]))
AC_ARG_ENABLE([output-disk],AC_HELP_STRING([--disable-output-disk],[Disable disk writer]))

# Check if we can compile the enabled drivers:
# OSS driver
AC_MSG_CHECKING([for OSS driver])
if test ${enable_output_oss:-yes} = no; then
	AC_MSG_RESULT([disabled])
else
	AC_TRY_CPP([[#include <fcntl.h>\
	#include <sys/ioctl.h>\
	#include <sys/soundcard.h>]],
		 AC_DEFINE(DRIVER_OSS,1,[Build OSS driver])
		 drivers=$drivers' oss.$(OBJEXT)'
		 AC_MSG_RESULT([enabled]),
		 AC_MSG_RESULT([no OSS headers found! -- disabled]))
fi

# Null output
AC_MSG_CHECKING([for null output])
if test ${enable_output_null:-yes} = no; then
	AC_MSG_RESULT([disabled])
else
   AC_DEFINE(DRIVER_NULL,1,[Build null output])
   AC_MSG_RESULT([enabled])
fi

# Disk writer
AC_MSG_CHECKING([for disk writer])
if test ${enable_output_disk:-yes} = no; then
	AC_MSG_RESULT([disabled])
else
   AC_DEFINE(DRIVER_DISK,1,[Build disk writer])
   drivers=$drivers' disk.$(OBJEXT)'
   AC_MSG_RESULT([enabled])
fi

AC_SUBST([drivers])

AC_OUTPUT
